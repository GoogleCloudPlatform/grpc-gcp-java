plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.google.protobuf' version '0.9.4'
    id 'com.github.sherter.google-java-format' version '0.9'
    id 'org.jreleaser' version '1.20.0'
}

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/maven2/" }
    mavenCentral()
    mavenLocal()
}

version = '1.8.1-SNAPSHOT'
group = 'com.google.cloud'
description = 'GRPC-GCP-Extension Java'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

def title = 'gRPC extension library for Google Cloud Platform'
def grpcVersion = '1.69.0'
def protobufVersion = '3.25.5'
def protocVersion = '3.25.5'
def spannerVersion = '6.1.0'
def opencensusVersion = '0.28.3'

dependencies {
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.opencensus:opencensus-api:${opencensusVersion}"
    implementation libs.opentelemetry.api
    implementation "com.google.api:api-common:2.1.5"
    implementation libs.auto.value.annotations

    annotationProcessor libs.auto.value

    testImplementation "com.google.auth:google-auth-library-credentials:0.13.0"
    testImplementation "com.google.cloud:google-cloud-spanner:${spannerVersion}"
    testImplementation "com.google.cloud:google-cloud-bigtable:0.107.0"
    testImplementation "com.google.api.grpc:proto-google-cloud-bigtable-v2:0.39.0"
    testImplementation "com.google.api.grpc:grpc-google-cloud-bigtable-v2:0.39.0"
    testImplementation "com.google.auth:google-auth-library-oauth2-http:0.9.0"
    testImplementation "io.grpc:grpc-auth:${grpcVersion}"
    // Align grpc-alts with the grpc-netty-shaded version to avoid runtime NoSuchMethodError in tests.
    testImplementation "io.grpc:grpc-alts:${grpcVersion}"
    testImplementation "io.grpc:grpc-stub:${grpcVersion}"
    testImplementation "com.google.truth:truth:0.42"
    testImplementation "org.mockito:mockito-core:4.11.0"
    testImplementation 'junit:junit:4.13.2'
    testImplementation libs.opentelemetry.sdk
    testImplementation libs.opentelemetry.sdk.metrics
    testImplementation libs.opentelemetry.sdk.testing
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

test {
    testLogging {
        outputs.upToDateWhen {false}
        showStandardStreams = true
    }
    exclude 'com/google/cloud/grpc/SpannerIntegrationTest.class'
    exclude 'com/google/cloud/grpc/BigtableIntegrationTest.class'
}

task allTests( type: Test ) {
    // Execute all the tests, including integration tests (Spanner/Bigtable).
    group = 'verification'
    description = 'Runs all tests, including integration tests.'

    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    testLogging {
        outputs.upToDateWhen {false}
        showStandardStreams = true
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar
            groupId = group
            artifactId = 'grpc-gcp'
            version = version
            pom {
                name = title
                url = 'https://github.com/GoogleCloudPlatform/grpc-gcp-java/tree/master/grpc-gcp'
                afterEvaluate {
                    // description is not available until evaluated.
                    description = project.description
                }

                scm {
                    connection = 'scm:git:https://github.com/GoogleCloudPlatform/grpc-gcp-java.git'
                    url = 'https://github.com/GoogleCloudPlatform/grpc-gcp-java'
                }

                licenses {
                    license {
                        name = 'Apache 2.0'
                        url = 'https://opensource.org/licenses/Apache-2.0'
                    }
                }

                developers {
                    developer {
                        id = "grpc.io"
                        name = "gRPC Contributors"
                        email = "grpc-io@googlegroups.com"
                        url = "https://grpc.io/"
                        organization = "gRPC Authors"
                        organizationUrl = "https://www.google.com"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    gitRootSearch = true
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
    release {
        github {
            skipRelease = true
        }
    }
}

googleJavaFormat {
  toolVersion = '1.26.0'
  exclude '.gradle/*'
  exclude 'bin/generated-sources/*'
}

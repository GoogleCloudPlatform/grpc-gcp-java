plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.4'
}

group 'io.grpc'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

def gcsioVersion = '2.2.26'
def grpcVersion = '1.71.0'
def protobufVersion = '3.25.5'
def protocVersion = protobufVersion
def conscryptVersion = '2.5.2'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation "io.grpc:grpc-alts:${grpcVersion}"
    implementation "io.grpc:grpc-api:${grpcVersion}"
    implementation "io.grpc:grpc-auth:${grpcVersion}"
    implementation "io.grpc:grpc-census:${grpcVersion}"
    implementation "io.grpc:grpc-context:${grpcVersion}"
    implementation "io.grpc:grpc-core:${grpcVersion}"
    implementation "io.grpc:grpc-googleapis:${grpcVersion}"
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf-lite:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-testing:${grpcVersion}"
    implementation "io.grpc:grpc-xds:${grpcVersion}"
    implementation "org.conscrypt:conscrypt-openjdk-uber:${conscryptVersion}"
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    implementation "com.google.api.grpc:grpc-google-cloud-storage-v2:latest.release"
    implementation "com.google.api.grpc:proto-google-common-protos:latest.release"
    implementation "com.google.api.grpc:proto-google-iam-v1:latest.release"
    implementation "com.google.apis:google-api-services-storage:latest.release"
    implementation "com.google.auth:google-auth-library-oauth2-http:latest.release"
    implementation "com.google.cloud:google-cloud-storage:latest.release"
    implementation "com.google.cloud.bigdataoss:bigdataoss-parent:${gcsioVersion}"
    implementation "com.google.cloud.bigdataoss:gcsio:${gcsioVersion}"
    implementation "com.google.guava:guava:32.1.3-jre"
    implementation "net.sourceforge.argparse4j:argparse4j:0.9.0"

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

task run(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'io.grpc.gcs.TestMain'
    //jvmArgs = ['-Djava.util.logging.config.file=./logging.properties']
    //jvmArgs = ['-Xlint:deprecation']
    //jvmArgs = ['-Djava.security.debug=provider']
}

task fatJar(type: Jar) {
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
                   'Implementation-Version': version,
                   'Main-Class': 'io.grpc.gcs.TestMain'
    }
    baseName = project.name + '-all'
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

task copyJarsToLib (type: Copy) {
    def toDir = "build/libs/dependency-jars"

    // create directories, if not already done:
    file(toDir).mkdirs()

    // copy jars to lib folder:
    from configurations.implementation
    into toDir
}

jar {
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'
    manifest {
        attributes (
            'Main-Class': 'io.grpc.gcs.TestMain',
            'Class-Path': '. dependency-jars/' + configurations.implementation.collect { it.getName() }.join(' dependency-jars/')
        )
    }
}

jar.dependsOn copyJarsToLib
